#!/bin/zsh
zmodload zsh/system
task="$1";shift
case "$task" in
	recompile) LOCK="$task";;
	exec) LOCK="$1";;
	*) LOCK="$*";;
esac
LOCKFILE="${XDG_RUNTIME_DIR:-/run/user/$UID}/ztasklocked/$LOCK.lck"
[[ -d "${LOCKFILE:h}" ]] || rm -f "${LOCKFILE:h}"
mkdir -p "${LOCKFILE:h}"
touch "$LOCKFILE"
exec <&- >&- 2>&-
zsystem flock -e -t 0 -f fd "$LOCKFILE" || exit
trap 'rm -f "$LOCKFILE"' EXIT

case "$task" in
	(recompile)
	setopt null_glob extended_glob
	autoload -Uz zcompare
	function zrecompile {
		local mode e
		mode="$1"; shift
		while (($# > 0)); do
			e="$1"; shift
			[[ -O $e:P:h ]] || continue
			[[ $e:e == zwc || $e:e == izwc ]] && continue

			if [[ -f $e ]];then
				zcompare "$e" "$mode"
			elif [[ -d $e ]];then
				zrecompile "$mode" "$e"/*
			fi
		done
	}
	zrecompile -Rz $@
	zrecompile -Mz \
		"${ZDOTDIR:-$HOME}"/.z{shrc,shenv,login,logout,profile,inputrc} \
		"${XDG_CACHE_HOME:-$HOME/.cache}/zsh"/**/*(.)
	wait
	;;
	(exec) "$@";;
esac

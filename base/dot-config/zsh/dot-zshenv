#!/bin/zsh
skip_global_compinit=1

fpath=(
	${ZDOTDIR:-$HOME/.zsh}/functions
	${ZDOTDIR:-$HOME/.zsh}/functions/compdefs
	$fpath
)

() {
	if [[ ${CONTAINER_ID/#%distrobox-[a-zA-Z0-9]*/distrobox-ephemeral} == distrobox-ephemeral ]]
	then ZSH_CACHE_DIR="/tmp/zsh"
	elif [[ $CONTAINER_ID ]]
	then ZSH_CACHE_DIR="${XDG_CACHE_HOME:-$HOME/.cache}/zsh/$CONTAINER_ID"
	else ZSH_CACHE_DIR="${XDG_CACHE_HOME:-$HOME/.cache}/zsh"
	fi
	[[ -d ${ZSH_CACHE_DIR} ]] || mkdir -p "$ZSH_CACHE_DIR"
}

ZSH="$ZDOTDIR/omz"
ZSH_COMPDUMP="$ZSH_CACHE_DIR/compdump"
function compinit {
		local body
		body=$functions[compinit]
		builtin unfunction compinit
		builtin autoload -Uz compinit
		compinit -d "$ZSH_COMPDUMP" "$@"
		functions[compinit]=$body
}

autoload -Uz zcompare

function load {
	local each from
	from=$1;shift
	for each in "$@";do
		[[ ! -d "${ZDOTDIR:-$HOME/.zsh}/$from.d/$each" ]] || each="$each/load"
		[[ -f "${ZDOTDIR:-$HOME/.zsh}/$from.d/$each" ]] || continue
		Z_COMPILE_TARGET="$from/$each" zcompare "${ZDOTDIR:-$HOME/.zsh}/$from.d/$each" -UMz
		source "${ZDOTDIR:-$HOME/.zsh}/$from.d/$each"
	done
}

if [ ! -d ~/.cache/zsh ];then
	rm -f ~/.cache/zsh
	mkdir -p ~/.cache/zsh
fi
# Traps
function TRAPUSR1 { rehash; }

unfunction zcompare

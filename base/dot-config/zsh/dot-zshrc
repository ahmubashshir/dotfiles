#!/bin/zsh
if [ -e ~/zprof ] || [ -p ~/pipe:zsh ];then
	zmodload zsh/datetime
	setopt PROMPT_SUBST
	PS4='+$EPOCHREALTIME %N:%i> '
	if [ -p ~/pipe:zsh ];then
		logfile=~/pipe:zsh
	else
		logfile=$(mktemp /tmp/zsh_profile.XXXXXXXX)
	fi
	echo "Logging to $logfile"
	exec 3>&2 2>$logfile
	setopt XTRACE
fi
# source
source ~/.config/shell/import
## options
zsh_options=(
	# globbing
	extended_glob prompt_subst rematchpcre
	no_nomatch null_glob
	# cd
	auto_cd auto_pushd pushd_silent
	pushd_to_home pushd_ignore_dups pushd_minus
	# completions
	auto_menu glob_complete menu_complete no_list_ambiguous
	# misc
	beep c_bases no_flow_control rm_star_wait
	auto_continue hash_cmds short_loops
	interactive_comments octal_zeroes
)
setopt $zsh_options
unset zsh_options
bindkey -e

## Lazy load section
autoload -Uz add-zsh-hook compinit
autoload -Uz "${ZDOTDIR:-$HOME/.zsh}/functions"/^_*~*.zwc(-.:t) >&3 2>&1

# configure completion cache before all else
zstyle ':completion:*' use-cache true
zstyle ':completion:*' cache-path "$ZSH_CACHE_DIR/compcache"

load rc plugins/hooks plugins/init
zplug --path "$ZDOTDIR/omz/plugins"

typeset -A ENV # base config variable
load rc completion completion/defs keymap history modules \
		prompt prompt/transient plugins <&- # close input

if [ -e ~/zprof ];then
	exec 2>&3 3>/dev/null
	unsetopt XTRACE
fi

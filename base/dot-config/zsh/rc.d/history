#!/bin/zsh
function histfile:local {
	[[ $PWD != $HOME ]] || return
	local OLDHIST="$HISTFILE"
	fc -p "$PWD/hist"
	fc -R "$OLDHIST"
}

() {
	[[ "${HOST:r}" == "$HOST" ]] || set "${${HOST:r}/#%distrobox-[a-zA-Z0-9]*/distrobox-ephemeral}"
	set "${XDG_STATE_HOME:-$HOME/.local/state}/zsh${1+/$1}/history"

	[[ -d ${1:a:h} ]] || mkdir -p ${1:a:h}
	[[ "${HOST:r}" == "$HOST" ]] || fc -R "${1:a:h:h}/history"
	HISTFILE="${1:a}"
}

HISTSIZE=10000
SAVEHIST=2147483647
HISTDUP=erase
setopt \
	hist_fcntl_lock \
	inc_append_history \
	extended_history \
	hist_no_store \
	hist_reduce_blanks \
	share_history \
	hist_expire_dups_first \
	hist_ignore_dups \
	hist_ignore_all_dups \
	hist_ignore_space \
	hist_find_no_dups \
	hist_save_no_dups \
	hist_no_functions \
	hist_verify

function -hist:pwd:file {
	local DIR="$PWD"
	until [[ ! -w $DIR || $DIR == '/' || $DIR == "$HOME" ]]; do
		if
			[[ -f "$DIR/hist" && $HISTFILE:t == "history" ]] ||
			[[ $HISTFILE:t = "hist" && ! $DIR -pcre-match ^${HISTFILE:h} ]]
		then
			fc -P
			if [[ -f "$DIR/hist" ]]
			then
				local OLDHIST="$HISTFILE"
				fc -p "$DIR/hist"
				fc -R "$OLDHIST"
			fi
			fc -R
			return
		fi
		DIR="${DIR:h}"
	done
}

add-zsh-hook -Uz chpwd -hist:pwd:file
cd "$PWD"

#!/bin/zsh
[[ $(get_term) =~ ^(.*geany.*|octopi)$ ]] && unset HISTFILE && return
→zsh/print/hist()
{
	echo 'HISTFILE: '"${${${${${HISTFILE/#${XDG_STATE_HOME:-$HOME\/.local\/state}/@STATE}/#${XDG_CONFIG_HOME:-$HOME\/.config}/@CONFIG}/#${XDG_CACHE_HOME:-$HOME\/.cache}/@CACHE}/#${XDG_DATA_HOME:-$HOME\/.local\/share}/@DATA}/#$HOME/~}"
}

function dirhist {
	[[ $PWD != $HOME ]] || return
	local OLDHIST="$HISTFILE"
	fc -p "$PWD/hist"
	fc -R "$OLDHIST"
	→zsh/print/hist
}

function nullhist {
	if [[ $HISTFILE == /dev/null ]]; then
		fc -P
		fc -R
		→zsh/print/hist
	else
		local OLDHIST="$HISTFILE"
		fc -p /dev/null
		fc -R "$OLDHIST"
		echo "HISTFILE: none"
	fi
}

() {
	[[ "${HOST:r}" == "$HOST" ]] || set "${${HOST:r}/#%distrobox-[a-zA-Z0-9]*/distrobox-ephemeral}"
	set "${XDG_STATE_HOME:-$HOME/.local/state}/zsh${1+/$1}/history"

	[[ -d ${1:a:h} ]] || mkdir -p ${1:a:h}
	[[ "${HOST:r}" == "$HOST" ]] || fc -R "${1:a:h:h}/history"
	HISTFILE="${1:a}"
}

HISTSIZE=10000
SAVEHIST=2147483647
HISTDUP=erase

setopt \
	hist_fcntl_lock \
	inc_append_history \
	extended_history \
	hist_no_store \
	hist_reduce_blanks \
	share_history \
	hist_ignore_all_dups \
	hist_ignore_space \
	hist_find_no_dups \
	hist_save_no_dups \
	hist_no_store \
	hist_no_functions \
	hist_verify

function →zsh/pwd/hist {
	local DIR="$PWD" OLDDIR=""
	! [[ $HISTFILE == /dev/null ]] || return

	: $HISTFILE:h
	until [[ $DIR == / || $DIR == $HISTFILE:h || $DIR == $HOME && ! $HISTFILE:t == hist ]]; do
		if
			[[ -f "$DIR/hist" && $HISTFILE != $DIR/hist || $DIR == $HOME && $HISTFILE:t == hist ]]
		then
			fc -P
			if [[ -f "$DIR/hist" && $DIR != $HOME ]]
			then
				local OLDHIST="$HISTFILE"
				fc -p "$DIR/hist"
				fc -R "$OLDHIST"
			fi
			fc -R
			→zsh/print/hist
			return
		fi
		OLDDIR="$DIR"
		DIR="${DIR:h}"
	done
}

add-zsh-hook -Uz chpwd →zsh/pwd/hist
→zsh/pwd/hist

#!/bin/zsh
function -hist:set:file {
	[[ -d $1:h ]] || mkdir -p $1:h
	[[ -z $HISTFILE ]] || fc -R
	HISTFILE="$1"
}

-hist:set:file "${XDG_STATE_HOME:-$HOME/.local/state}/zsh/history"
[[ "${HOST:r}" == "$HOST" ]] || -hist:set:file "${HISTFILE:r:h}/${HOST:r}/history"

HISTSIZE=10000
SAVEHIST=2147483647
HISTDUP=erase
setopt \
	hist_fcntl_lock \
	inc_append_history \
	extended_history \
	hist_no_store \
	hist_reduce_blanks \
	share_history \
	hist_expire_dups_first \
	hist_ignore_dups \
	hist_ignore_all_dups \
	hist_ignore_space \
	hist_find_no_dups \
	hist_save_no_dups \
	hist_no_functions \
	hist_verify

function -hist:pwd:file {
	if [[ -f "$PWD/hist" ]]
	then [[ $HISTFILE:t = "history" ]] || fc -P; local OLDHIST="$HISTFILE"
		fc -p "$PWD/hist"; fc -R "$OLDHIST"; fc -R
	elif [[ $HISTFILE:t = "hist" && ! $PWD -pcre-match ^${HISTFILE:h} ]]
	then fc -P
	fi
}

add-zsh-hook -Uz chpwd -hist:pwd:file

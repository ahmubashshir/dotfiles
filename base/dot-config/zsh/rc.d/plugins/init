#!/bin/zsh
local -a ___plugins

___plugins=(
	"$ZDOTDIR/.zshrc"(:P:h:h:h:h)
	"$ZDOTDIR/plugins"/*/.git(-/:P:t5:h)
	"$ZDOTDIR/omz"(:P:t4)
)

if ((${#___plugins[@]} > 1));then
	if ! zplug defer;then
		git -C "${___plugins[1]}" submodule init --quiet "${___plugins[@]:1}"
		if ! git -C "${___plugins[1]}" submodule update --progress "${___plugins[@]:1}"
		then
			printf "zsh: failed to initialize plugins\n"
			function zsh-defer {
				while [[ $1 =~ ^[+-] ]];do shift; done
				"$@"
			}
		else
			printf "zsh: finished cloning modules\n"
			zplug defer
		fi
	else
		"$ZDOTDIR/ztasklocked" exec \
			git -C "${___plugins[1]}" submodule update --progress "${___plugins[@]:1}" &!
	fi
	unset ___plugins
fi <&- # close input stream

#compdef dots
__dots_list_commands() {
	local -a commands
	commands=(
		'ln:link dotfiles.'
		'ls:list dotfiles.'
		'ps:use chr as seperator.'
		'cp:track path.'
		'rm:unlink and delete dotfile.'
		'systemd:update/sync systemd user config.'
		'help:show this help.'
	)
	_describe 'command' commands
}

__dots_list_dotfiles() {
	local dotdir="$(dirname "$(realpath "$(which dots)")")"
	local -a dotfiles
	dotfiles=("$dotdir"/dot-*(:t))
	if ((${#dotfiles} + 1 < ${#line})); then
		_message 'no more arguments'
	else
		_message 'dotfiles'
		_multi_parts $(grep '^PATHSEP' "$dotdir/dots"|cut -d= -f2) dotfiles
	fi
}

__dots_systemd_cmds() {
	local -a actions
	actions=(
		'backup:backup systemd user config.'
		'restore:restore systemd user config.'
	)
	_describe 'action' actions
}

__dots_command_args() {
	case $line[1] in
		ln | ls | help) _message 'no more arguments' ;;
		ps) _arguments '2:seperator:' ;;
		cp) _files -g '.*' ;;
		systemd) _arguments "2:action:__dots_systemd_cmds" ;;
		rm) __dots_list_dotfiles ;;
	esac
}

local args state
args=(
	"1:command:__dots_list_commands"
	"*:args:__dots_command_args"
)
_arguments -s "${args[@]}"

# vim: ft=sh:ts=4:noet:

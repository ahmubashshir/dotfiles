#compdef reboot-to

__reboot_to_list_targets() {
	local RESET BOLD BLUE GREEN RED YELLOW

		CSI=$'\e['
	   BOLD="${CSI}1m"
		RED="${BOLD}${CSI}31m"
	  GREEN="${BOLD}${CSI}32m"
	 YELLOW="${BOLD}${CSI}33m"
	   BLUE="${BOLD}${CSI}34m"
	MAGENTA="${BOLD}${CSI}35m"
	   CYAN="${BOLD}${CSI}36m"
	  WHITE="${BOLD}${CSI}37m"
	  RESET="${CSI}0m"

	[[ $(bootctl is-installed) == "yes" ]] || return
	[[ $(command -pv jq) ]] || return

	local -A targets
	local bctl_hash bctl_hash_cached
	bctl_hash=${"$(bootctl list --json=short|sha256sum)":0:63}
	fun_hash=${"$(sha256sum <<< ${functions[__reboot_to_list_targets]})":0:63}

	# retrive and check cache
	_retrieve_cache reboot-to
	if ! [[ $bctl_hash == $bctl_hash_cached && $fun_hash == $fun_hash_cached  ]];then
		# reset variables
		_cache_invalid reboot-to
		unset targets bctl_hash_cached fun_hash_cached
		local -A desc targets
		local id source fsroot file title bctl_hash_cached fun_hash_cached
		bctl_hash_cached=$bctl_hash
		fun_hash_cached=$fun_hash

		# rebuild data
		while IFS=$'\t' read -r id title source;do
			case $id in
				(efi:windows)	COLOR="$MAGENTA";;
				(efi:*)			COLOR="$RED";;
				(uki:*)			COLOR="$GREEN";;
				(cfg:*)
					fsroot="${source%loader/entries/*.conf}"
					case "$id" in
						(*:memtest*) COLOR="$RED";;
						(*linux-*)   COLOR="$BLUE";;
						(*)          COLOR="$CYAN";;
					esac
					while read -r file; do
						if [ ! -f "$fsroot$file" ]; then
							continue 2
						fi
					done < <(
						awk '/^(initrd|linux|efi)/ {print $2}' "$source"
					)
				;;
			esac
			targets+=(["$id"]="${id//:/\:}:${COLOR}$title${RESET}")
		done < <(bootctl list --json=short |
		jq -r '[.[] | . * {
			id: (.id
				| sub("^arch-"; "")
				| if test("\\.efi$")
					then sub("^(?<id>.+)\\.efi$"; "uki:\(.id)")
				elif test("\\.conf$")
					then sub("^(?<id>.+)\\.conf$"; "cfg:\(.id)")
				elif test("^auto-")
					then sub("^auto(-reboot-to|-efi)?-(?<id>.+)$"; "efi:\(.id)")
				end)
			}
			| [(.id |
				if test(":windows$") then 2
				elif test("^efi:")   then 0
				elif test("memtest") then 1
				elif test("^uki:")   then 1
				elif test("linux-")  then 2
				elif test("^cfg:")   then 3
				end),
				.id,
				(if .id == "efi:firmware-setup"
                then "EFI Firmware Setup"
                elif (.id|test("^uki:(.+-)?linux(-fallback)?$"))
                then "\(.title), \(.version|sub("^\\d+\\.\\d+\\.\\d+-(?:\\w+)-\\d+"; "linux")|sub("~"; ", "))"
                elif (.id|test("^uki:"))
                then "\(.title), \(.version|sub("^\\d+\\.\\d+\\.\\d+(?:(-\\w+)?-\\d+)?-(?<id>\\w+)[^~]*"; "\(.id)")|sub("~"; ", "))"
                else .showTitle
				end),
				.path
			]] | sort[] | .[1:] | join("\t")'
		)
		# store cache
		_store_cache reboot-to targets bctl_hash_cached fun_hash_cached
	fi
	_describe "reboot targets" targets
}
if ! [[ $(bootctl is-installed) == "yes" ]]; then
	_message "systemd-boot is not installed"
	return
fi

if ! command -pv jq 2> /dev/null >&2; then
	_message "error: jq is not installed"
	return 1
fi

_arguments -s "1:reboot target:__reboot_to_list_targets"

# vim:ts=4:ft=sh:noet:

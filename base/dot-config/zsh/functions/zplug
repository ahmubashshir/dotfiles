#!/bin/zsh
local plugname plug plugpath plugin plugpkg
typeset -gA __zplug__rpath __zplug___rpkg

if (( ! $+functions[›zplug›hook]));then
		if (( ! ${+zplugdirs} )); then
		typeset -ga zplugdirs
	fi
	if (( ! ${+zsh_loaded_plugins} )); then
		typeset -gA zsh_loaded_plugins
	fi
	for plugpath in "${ZDOTDIR:-$HOME/.zsh}/plugins" "/usr/share/zsh/plugins";do
		if ! [[ ${zplugdirs[(r)$plugpath]} == $plugpath ]]; then
			zplugdirs+=("$plugpath")
		fi
	done
	while read -r plug _;do
		if (($+functions[$plug]));then
			function-mv $plug ›zplug›hook›${${plug#zplug:}:s+:+›+}
			unset -f $plug
		fi
	done < <(
		functions -m 'zplug:(pre|post):*'
	)
	autoload -Uz _cache_invalid _store_cache _retrieve_cache
	_retrieve_cache zplug-resolved
fi

if [[ $1 = --path ]];then
	if [[ -d $2 ]];then
		zplugdirs+=("$2")
	fi
	return
fi

if [[ -z $1 ]];then
	printf '%s\n' $zsh_loaded_plugins
	return
fi

function ›zplug›hook {
	local when=$1 plugin=$2;shift
	if (($+functions[›zplug›hook›${when}›$plugin]));then
		›zplug›hook›${when}›$plugin "$@"
	fi
}


function ›zplug›load {
	local plugname="$1" plugin="$2" plugpath="$3"; shift 3

	if [[ -w $plugin:P:h ]] && [[ -s "$plugin" && ( ! -s "$plugin.zwc" || "$plugin" -nt "$plugin.zwc" ) ]]
	then
		rm -f "$plugin.zwc"
		Z_COMPILE_TARGET="${plugpath:t2:h1}-$plugname" zcompile "$plugin"
	fi
	›zplug›hook pre  "$plugname" "$@" || return 1
	builtin source   "$plugin" "$@" || return 1
	›zplug›hook post "$plugname" "$@" || return 1
	if (( ! ${+zsh_loaded_plugins[$plugname]}));then
		zsh_loaded_plugins[$plugname]=$plugin:h
	fi
	return 0
}

plugname=$1:t
plugpkg=$1:h
[[ $plugpkg =~ ^\.$ ]] && unset plugpkg
shift

if [[  -n ${__zplug__rpath[$plugname]} \
	&& -n ${__zplug___rpkg[$plugname]}  \
	&& -f ${__zplug__rpath[$plugname]} \
	&& -d ${__zplug___rpkg[$plugname]}
]] && ›zplug›load "$plugname" "${__zplug__rpath[$plugname]}" "${__zplug___rpkg[$plugname]}"
then return 0
fi

unset "__zplug__rpath[$plugname]" "__zplug___rpkg[$plugname]"

for plugpath in $zplugdirs[@];do
	for plug in \
		"$plugname/$plugname.plugin" \
		"$plugname/zsh-$plugname.plugin" \
		"zsh-$plugname/$plugname.plugin" \
		"zsh-$plugname/zsh-$plugname.plugin" \
		"$plugname/$plugname" \
		"zsh-$plugname/$plugname" \
		"zsh-$plugname/zsh-$plugname" \
		"$plugname/init" \
		"zsh-$plugname/init"
	do
		plugin="$plugpath/${plugpkg+$plugpkg/}$plug.zsh"
		if [[ -f "$plugin" ]] && ›zplug›load "$plugname" "$plugin" "$plugpath"
		then
			__zplug__rpath[$plugname]="$plugin"
			__zplug___rpkg[$plugname]="$plugpath"
			_store_cache zplug-resolved __zplug___rpkg __zplug__rpath
			return 0
		fi
	done
done
return 1

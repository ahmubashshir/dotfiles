#!/bin/zsh
local plugname plug plugpath plugin plugpkg
if (( ! $+functions[____zplug_run_hook]));then
		if (( ! ${+zplugdirs} )); then
		typeset -ga zplugdirs
	fi
	if (( ! ${+zsh_loaded_plugins} )); then
		typeset -gA zsh_loaded_plugins
	fi
	for plugpath in "${ZDOTDIR:-$HOME/.zsh}/plugins" "/usr/share/zsh/plugins";do
		if ! [[ ${zplugdirs[(r)$plugpath]} == $plugpath ]]; then
			zplugdirs+=("$plugpath")
		fi
	done
	while read -r plug _;do
		if (($+functions[$plug]));then
			functions -c $plug →zplug/hooks/${${plug#zplug:}:s+:+/+}
			unset -f $plug
		fi
	done < <(
		functions -m 'zplug:(pre|post):*'
	)
fi

if [[ $1 = --path ]];then
	if [[ -d $2 ]];then
		zplugdirs+=("$2")
	fi
	return
fi

if [[ -z $1 ]];then
	printf '%s\n' $zsh_loaded_plugins
	return
fi

function ____zplug_run_hook {
	local when=$1 plugin=$2;shift
	if (($+functions[→zplug/hooks/$when/$plugin]));then
		→zplug/hooks/$when/$plugin "$@"
	fi
}

plugname=$1:t
plugpkg=$1:h
[[ $plugpkg =~ ^\.$ ]] && unset plugpkg
shift
for plugpath in $zplugdirs[@];do
	for plug in \
		"$plugname/$plugname.plugin" \
		"$plugname/zsh-$plugname.plugin" \
		"zsh-$plugname/$plugname.plugin" \
		"zsh-$plugname/zsh-$plugname.plugin" \
		"$plugname/$plugname" \
		"zsh-$plugname/$plugname" \
		"zsh-$plugname/zsh-$plugname" \
		"$plugname/init" \
		"zsh-$plugname/init"
	do
		plugin="$plugpath/${plugpkg+$plugpkg/}$plug.zsh"
		if [ -f "$plugin" ]
		then
			if [[ -w $plugin:P:h ]] && [[ -s "$plugin" && ( ! -s "$plugin.zwc" || "$plugin" -nt "$plugin.zwc" ) ]]
			then
				rm -f "$plugin.zwc"
				Z_COMPILE_TARGET="${plugpath:t2:h1}-$plugname" zcompile "$plugin"
			fi
			____zplug_run_hook pre $plugname "$@"
			builtin source "$plugin" "$@"
			____zplug_run_hook post $plugname "$@"
			if (( ! ${+zsh_loaded_plugins[$plugname]}));then
				zsh_loaded_plugins[$plugname]=$plugin:h
			fi
			return 0
		fi
	done
done
return 1

#!/bin/zsh
autoload -Uz zrecompile
emulate sh -c 'source /etc/profile'
if [ -f $HOME/.shell/environment ]; then
	emulate sh -c 'source '$HOME'/.shell/environment'
fi
if [ -f $HOME'/.profile' ];then
	emulate sh -c 'source '$HOME'/.profile'
fi
unfunction appendpath

if [ ! -d ~/.cache/zsh ];then
	rm -f ~/.cache/zsh
	mkdir -p ~/.cache/zsh
fi
if [[ -o interactive ]];then
	function TRAPPOLL {
		compinit -C
		[ -r $HOME/.zsh/compdef ] && source $HOME/.zsh/compdef
	}
	autoload -Uz compinit
fi
# Traps
function TRAPUSR1 { rehash; }

# misc functions
function zcompinit {
	if [[ -o interactive ]];then
		compinit $@
		[[ $* =~ -[[:alnum:]]*C ]] || {
			local n k
			for n in $fpath[@];do
				if [[ -f $n ]] && [[ -w $n:P:h ]];then
					zcompare "$n"
				elif [[ -d $n ]] && [[ -w $n ]];then
					for k in "$n"/^*.zwc(^/);do
						zcompare "$k"
					done
				fi
			done
			sleep 0.5
			kill -POLL $$
		} &!
	fi
}

function safe-source {
	if [ -f "$1" ];then
		source "$@"
	fi
}
function zrecompile_start {
	for n in ~/.z{shrc,shenv,login,login,profile,inputrc,sh/compdef};do
		if [ -f $n ];then
			zrecompile -p -R $n -- &!
		fi
	done
}
function zcompare {
	if [[ -w $1:P:h ]] && [[ -s ${1} && ( ! -s ${1}.zwc || ${1} -nt ${1}.zwc) ]]; then
		zcompile ${1}
	fi
}

function bashcompadd {
	for n in "$@";do
		if [ -f "/usr/share/bash-completion/completions/$n" ];then
			source "/usr/share/bash-completion/completions/$n"
		fi
	done
}
if ! [[ -d "${XDG_CACHE_HOME:-$HOME/.cache}/zsh/dirs" ]];then
	mkdir -p "${XDG_CACHE_HOME:-$HOME/.cache}/zsh/dirs"
fi
if tty -s;then
	DIRSTACKFILE="${XDG_CACHE_HOME:-$HOME/.cache}/zsh/dirs/${TTY##*/}"
	if [[ -f "$DIRSTACKFILE" ]] && (( ${#dirstack} == 0 )); then
		dirstack=("${(@f)"$(< "$DIRSTACKFILE")"}")
		[[ -d "${dirstack[1]}" ]] && cd -- "${dirstack[1]}"
	fi
fi
safe-source /usr/share/doc/pkgfile/command-not-found.zsh

#!/bin/zsh
alias compinit="compinit -d ~/.cache/zsh/compdump"
function zcompile {
	sed -E 's/^\./dot-/;s/(.*)/zcompile: \1/' <<<$argv[-1]:t >&2
	builtin zcompile "$@" &!
}
if [ ! -d ~/.cache/zsh ];then
	rm -f ~/.cache/zsh
	mkdir -p ~/.cache/zsh
fi
# Traps
function TRAPUSR1 { rehash; }

# misc functions
function zcompinit {
	if [[ -o interactive ]];then
		compinit $@
		safe-source $HOME/.zsh/compdef
		[[ $* =~ -[[:alnum:]]*C ]] || {
			local n k
			for n in $fpath[@];do
				if [[ -f $n ]] && [[ -w $n:P:h ]];then
					zcompare "$n"
				elif [[ -d $n ]] && [[ -w $n ]];then
					for k in "$n"/^*.zwc(^/);do
						zcompare "$k"
					done
				fi
			done
			compinit -C
			safe-source $HOME/.zsh/compdef
		}
	fi
}

function safe-source {
	if [ -f "$1" ];then
		source "$@"
	fi
}
function zrecompile_start {
	for n in ~/.z{shrc,shenv,login,login,profile,inputrc,sh/compdef};do
		if [ -f $n ];then
			zcompare $n -Rz
		fi
	done
}

function zcompare {
	if [[ -w $1:P:h ]] && [[ -s ${1} && ( ! -s ${1}.zwc || ${1} -nt ${1}.zwc) ]]; then
		zcompile "${@:2}" "$1"
	fi
}
if [[ -o interactive ]];then
	autoload -Uz compinit
	safe-source "$HOME/.zinputrc"
	safe-source $HOME'/.shell/import'
	[ -z "$(get_term|grep 'geany')" ] && HISTFILE=${HISTFILE:-~/.zsh_history}
fi
safe-source $HOME'/.shell/environment'
safe-source /usr/share/doc/pkgfile/command-not-found.zsh
safe-source ./.env_setup

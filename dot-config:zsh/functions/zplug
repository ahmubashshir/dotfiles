#!/bin/zsh
local plugname plug plugpath plugin
[[ -z $1 ]] && return 1

if (( ! ${+zplugdirs} )); then
	typeset -ga zplugdirs
fi
if (( ! ${+zsh_loaded_plugins} )); then
	typeset -ga zsh_loaded_plugins
fi
if (( ! ${+zsh_loaded_plugin_paths} )); then
	typeset -ga zsh_loaded_plugin_paths
fi
for plugpath in "${ZDOTDIR:-$HOME/.zsh}/plugins" "/usr/share/zsh/plugins";do
	if ! [[ ${zplugdirs[(r)$plugpath]} == $plugpath ]]; then
		zplugdirs+=("$plugpath")
	fi
done

plugname=$1;shift
for plugpath in $zplugdirs[@];do
	for plug in "$plugname/$plugname.plugin" \
				"$plugname/zsh-$plugname.plugin" \
				"zsh-$plugname/$plugname.plugin" \
				"zsh-$plugname/zsh-$plugname.plugin" \
				"$plugname/$plugname" \
				"zsh-$plugname/$plugname" \
				"zsh-$plugname/zsh-$plugname"
	do
		plugin="$plugpath/$plug.zsh"
		if [ -f "$plugin" ]
		then
			if [[ -w $plugin:P:h ]] && [[ -s "$plugin" && ( ! -s "$plugin.zwc" || "$plugin" -nt "$plugin.zwc" ) ]]
			then
				rm -f "$plugin.zwc"
				zcompile "$plugin"
			fi
			builtin source "$plugin" "$@"
			if ((!${zsh_loaded_plugins[(Ie)$plugname]}));then
				zsh_loaded_plugins+="$plugname"
				zsh_loaded_plugin_paths+="${plugin:h}"
			fi
			return 0
		fi
	done
done
return 1

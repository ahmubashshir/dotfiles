#!/bin/zsh
local plugname plug plugpath plugin
[[ -z $1 ]] && return 1

if (( ! ${+zplugdirs} )); then
	typeset -ga zplugdirs
fi
if (( ! ${+zsh_loaded_plugins} )); then
	typeset -ga zsh_loaded_plugins
fi

for plugpath in "${ZDOTDIR:-$HOME/.zsh}/plugins" "/usr/share/zsh/plugins";do
	if ! [[ ${zplugdirs[(r)$plugpath]} == $plugpath ]]; then
		zplugdirs+=("$plugpath")
	fi
done

plugname=$1;shift
for plugpath in $zplugdirs[@]
do
	for plug in "$plugpath/$plugname/$plugname.plugin" \
				"$plugpath/$plugname/zsh-$plugname.plugin" \
				"$plugpath/zsh-$plugname/$plugname.plugin" \
				"$plugpath/zsh-$plugname/zsh-$plugname.plugin" \
				"$plugpath/$plugname/$plugname" \
				"$plugpath/zsh-$plugname/$plugname" \
				"$plugpath/zsh-$plugname/zsh-$plugname"
	do
		plugin="$plug.zsh"
		if [ -f "$plugin" ]
		then
			if [[ -w $plugin:P:h ]] && [[ -s "$plugin" && ( ! -s "$plugin.zwc" || "$plugin" -nt "$plugin.zwc" ) ]]
			then
				rm -f "$plugin.zwc"
				zcompile "$plugin"
			fi
			builtin source "$plugin" "$@"
			if ((!${zsh_loaded_plugins[(Ie)$plugname]}));then
				zsh_loaded_plugins+="$plugname"
			fi
			return 0
		fi
	done
done
return 1

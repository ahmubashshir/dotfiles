#compdef zplug
_list_plugins()
{
	local -a plugins
	plugins=()
	local plugpath i j k
	if ! (( ${+zplugdirs} )); then
		typeset -ga zplugdirs
	fi
	for plugpath in "${ZDOTDIR:-$HOME/.zsh}/plugins" "/usr/share/zsh/plugins";do
		if ! [[ ${zplugdirs[(r)$plugpath]} == $plugpath ]]; then
			zplugdirs+=("$plugpath")
		fi
	done
	for plugpath in $zplugdirs[@];do
		for i in "$plugpath"/*(/);do
			i=${i:t}
			j=${i#zsh-}
			for plug in "$j.plugin" "zsh-$j.plugin" \
				"$j.plugin" "zsh-$j.plugin" "$j" "zsh-$j"
			do
				if [ -f "$plugpath/$i/$plug.zsh" ];then
					plugins+=($j)
					continue 2
				fi
			done
			for k in "$plugpath/$i"/*(/);do
				k=${k:t}
				j=${k#zsh-}
				for plug in "$j.plugin" "zsh-$j.plugin" \
					"$j.plugin" "zsh-$j.plugin" "$j" "zsh-$j"
				do
					if [ -f "$plugpath/$i/$k/$plug.zsh" ];then
						plugins+=($i/$j)
						continue 2
					fi
				done
			done
		done
	done
	_values plugin $plugins
}

local args
args=(
    "1:plugin:_list_plugins"
    "*::plugin arguments:"
)
_arguments -s "${args[@]}"

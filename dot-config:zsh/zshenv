#!/bin/zsh
fpath=(${ZDOTDIR:-$HOME/.zsh}/functions $fpath)

function source {
	if [ -f "$1" ];then
		builtin source "$@"
	fi
}

function compinit {
		local body
		body=$functions[compinit]
		unfunction compinit
		autoload -Uz compinit
		compinit -d "${XDG_CACHE_HOME:-$HOME/.cache}/zsh/compdump" "$@"
		functions[compinit]=$body
}

if [[ -o interactive ]];then
	autoload -Uz zcompare
else
	function zcompare {
		if [[ -w $1:P:h ]] && [[ -s "$1" && ( ! -s "$1.zwc" || "$1" -nt "$1.zwc" ) && ! -f "$1.ignore.zwc" ]]
		then
			zcompile "${@:2}" "$1"
		fi
	}
fi

function loadenv {
	local each
	for each in "$@";do
		zcompare "${ZDOTDIR:-$HOME/.zsh}/env.d/$each" -UMz
		source "${ZDOTDIR:-$HOME/.zsh}/env.d/$each"
	done
}
if [ ! -d ~/.cache/zsh ];then
	rm -f ~/.cache/zsh
	mkdir -p ~/.cache/zsh
fi
# Traps
function TRAPUSR1 { rehash; }
loadenv api-keys common cli path
[[ -n $DISPLAY ]] && loadenv gui

unfunction zcompare loadenv
